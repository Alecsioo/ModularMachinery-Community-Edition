plugins {
    id 'net.minecraftforge.gradle' version '5.+'
    id 'wtf.gofancy.fancygradle' version '1.+'
}

version = "1.12.2-1.11.1-r46-dev"
group = "hellfirepvp.modularmachinery"

repositories {
    maven {
        url 'https://maven.aliyun.com/nexus/content/groups/public/'
    }
    maven {
        url 'https://maven.aliyun.com/nexus/content/repositories/jcenter'
    }
    maven {
        url 'https://maven.cleanroommc.com'
    }
    maven {
        url 'https://cfa2.cursemaven.com'
    }
    maven {
        url 'https://cursemaven.com'
    }
    maven {
        url 'https://maven.blamejared.com/'
    }
//    No longer available
//    maven {
//        url 'https://dvs1.progwml6.com/files/maven/'
//    }
    maven {
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }
    maven {
        url 'https://maven.hellfiredev.net/'
    }
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
}

java {
    archivesBaseName = 'ModularMachinery-CE'
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        implementation = JvmImplementation.VENDOR_SPECIFIC
    }
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                moudlarmachinery {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                modularmachinery {
                    source sourceSets.main
                }
            }
        }
    }
}

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    implementation "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.+"
    implementation fg.deobf("curse.maven:had-enough-items-557549:4506796")
    implementation fg.deobf("curse.maven:zenutil-401178:4394263")
    implementation fg.deobf("curse.maven:RedstoneFlux-270789:2920436")
    implementation fg.deobf("curse.maven:CodeChickenLib-242818:2779848")
    implementation fg.deobf("curse.maven:brandonscore-231382:3051539")
    implementation fg.deobf("curse.maven:draconicevolution-223565:3051542")
    implementation fg.deobf("curse.maven:the-one-probe-245211:2667280")
    implementation fg.deobf("curse.maven:Mekanism-268560:2835175")
    compileOnly fg.deobf("curse.maven:industrialcraft-2-242638:3078604")
    compileOnly fg.deobf("curse.maven:nuclearcraft-overhauled-336895:3862197")
    implementation fg.deobf("curse.maven:flux-networks-248020:3178199")
    compileOnly fg.deobf("curse.maven:tinkers-construct-74072:2902483")
    compileOnly fg.deobf("curse.maven:mantle-74924:2713386")
    implementation fg.deobf("curse.maven:tx-loader-706505:4515357")
    implementation fg.deobf("curse.maven:applied-energistics-2-223794:2747063")
    compileOnly fg.deobf("curse.maven:gregtech-293327:3266351")
    //Because GTCEu 2.4 and above and NCo crashed, this is to use GTCEu 2.3.4 for testing
    //Rendering errors occur when the GTCEu version is tested running, so just compileOnly
    compileOnly fg.deobf("curse.maven:gregtech-ce-unofficial-557242:3859788")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "modularmachinery",
                "Specification-Vendor"    : "hellfirepvp",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "hellfirepvp",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
